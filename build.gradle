subprojects {
    group = 'pl.rtprog'
    description="Java classes to JavaScript Flow types converter/generator"
    version="0.0.4"

    plugins.withType(JavaPlugin) {
        task javadocJar(type: Jar) {
            archiveClassifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }
    }

    plugins.withType(MavenPublishPlugin) {
//        artifacts {
//            archives javadocJar, sourcesJar
//        }
        if(name=="java2flow-gradle-plugin") return

        publishing {
            repositories {
                maven {
                    url=version.endsWith('SNAPSHOT')?"https://s01.oss.sonatype.org/content/repositories/snapshots/"
                            :"https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                    credentials {
                        username= findProperty('ossrhUsername')
                        password= findProperty('ossrhPassword')
                    }
                }
            }

            publications {
                mavenJava(MavenPublication) {
                    from(components.java)
                    artifact sourcesJar
                    artifact javadocJar

                    pom {
                        url = 'https://github.com/Ryszard-Trojnacki/java2flow'
                        packaging = 'jar'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'ryszard-trojnacki'
                                name = 'Ryszard Trojnacki'
                                email = 'ryszard.trojnacki@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git://github.com/Ryszard-Trojnacki/java2flow.git'
                            developerConnection = 'scm:git://github.com/Ryszard-Trojnacki/java2flow.git'
                            url = 'https://github.com/Ryszard-Trojnacki/java2flow'
                        }
                    }

                }
            }
        }
    }

    plugins.withType(SigningPlugin) {
        if(findProperty('ossrhUsername')!=null) {
            signing {
                sign publishing.publications.mavenJava
            }
        }
    }
}