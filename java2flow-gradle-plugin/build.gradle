plugins {
    id 'java-gradle-plugin'
    id 'idea'
    id 'com.gradle.plugin-publish' version '1.1.0'
}

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":java2flow-core")
    implementation project(":java2flow-javadoc-reader")
    implementation project(":java2flow-class-annotation")

    implementation group: 'org.jetbrains', name: 'annotations', version: '24.0.1'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'


    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
}

gradlePlugin {
    website = 'https://github.com/Ryszard-Trojnacki/java2flow'
    vcsUrl = 'https://github.com/Ryszard-Trojnacki/java2flow'
    plugins {
        java2flow {
            id = 'pl.rtprog.java2flow'
            implementationClass = 'pl.rtprog.java2flow.Java2FlowPlugin'
            displayName = 'Java2Flow Gradle plugin'
            description = 'Gradle plugin for converting Java structures to JavaSript with Flow types'
            tags.set([ 'flow', 'flow.org', 'javascript', 'java2javascript', 'java2flow' ])
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)


tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    // Use JUnit Jupiter for unit tests.
    useJUnitPlatform()
}
