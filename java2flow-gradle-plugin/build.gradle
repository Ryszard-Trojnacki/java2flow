plugins {
    id 'java-gradle-plugin'
    id 'idea'
    id 'maven-publish'
}

java {
    sourceCompatibility = 8
    targetCompatibility = 8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":java2flow-core")
    implementation project(":java2flow-javadoc-reader")
    implementation group: 'org.jetbrains', name: 'annotations', version: '23.0.0'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'


    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

gradlePlugin {
    plugins {
        java2flow {
            id = 'pl.rtprog.java2flow'
            implementationClass = 'pl.rtprog.java2flow.Java2FlowPlugin'
            displayName = 'Java2Flow Gradle plugin'
            description = 'Gradle plugin for converting Java structures to JavaSript Flow'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)


tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    // Use JUnit Jupiter for unit tests.
    useJUnitPlatform()
}
